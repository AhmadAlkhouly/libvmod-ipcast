varnishtest "Test ipcast vmod"

server s1 {
       rxreq
       txresp
} -start

varnish v1 -vcl+backend {
	import ipcast from "${vmod_topbuild}/src/.libs/libvmod_ipcast.so";
	sub vcl_recv {
                set req.http.x-client = regsub(req.http.X-Forwarded-For, "^(^[^,]+),?.*$", "\1");
                if (ipcast.ip(req.http.x-client, "198.51.100.255") == "198.51.100.255") { error 400 "Bad request"; }
	}
	sub vcl_deliver {
		set resp.http.x-ip = ipcast.ip(req.http.x-client, "198.51.100.255");
	}
} -start

client c1 {
	txreq -url "/" -hdr "X-Forwarded-For: 192.168.101.227, 172.16.231.30"
	rxresp
	expect resp.http.x-ip == "192.168.101.227"

	txreq -url "/" \
		-hdr "X-Forwarded-For: 2a00:1028:83a2:4542:8d6f:1a29:8f2d:df84"
	rxresp
	expect resp.http.x-ip == "2a00:1028:83a2:4542:8d6f:1a29:8f2d:df84"

	# reduced size form.
	txreq -url "/" -hdr "X-Forwarded-For: 2001:67c:2804:1001::c21f:27a4"
	rxresp
	expect resp.http.x-ip == "2001:67c:2804:1001::c21f:27a4"
} -run
